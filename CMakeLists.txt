cmake_minimum_required( VERSION 2.8.4 FATAL_ERROR )

PROJECT(cuvnet CXX C)
SET(CMAKE_MODULE_PATH  ${CMAKE_MODULE_PATH} ${CMAKE_SOURCE_DIR}/CMakeModules )
SET(PROJECT_NAME_STR cuvnet)

EXECUTE_PROCESS(COMMAND "date" "+%Y%m%d%H%M" OUTPUT_VARIABLE DATESTR)
string(REGEX REPLACE "^(....)(..)(..)(..)(..).*" "\\1\\2\\3\\4\\5" DATESTR ${DATESTR}) 

INCLUDE(InstallRequiredSystemLibraries)

SET(CPACK_SOURCE_GENERATOR "TGZ")
SET(CPACK_GENERATOR "DEB")
SET(CPACK_PACKAGE_NAME "cuvnet")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "A generic GPU-based function optimization architecture")
SET(CPACK_PACKAGE_CONTACT "Hannes Schulz <schulz at ais dot uni-bonn dot de>")
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README")
SET(CPACK_PACKAGE_VERSION_MAJOR "0")
SET(CPACK_PACKAGE_VERSION_MINOR "1")
SET(CPACK_PACKAGE_VERSION_PATCH ${DATESTR})
SET(CPACK_SOURCE_IGNORE_FILES "${CPACK_SOURCE_IGNORE_FILES};${CMAKE_SOURCE_DIR}/.git")
#SET(CPACK_PACKAGE_INSTALL_DIRECTORY "cuv")

set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} --std=c++0x -Wall")

SET(CPACK_DEBIAN_PACKAGE_DEPENDS    "libboost-dev (>= 1.40.0), cuv" )
INCLUDE(CPack)

INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/third_party/boost-numeric-bindings)

FIND_PACKAGE(Doxygen)
IF(DOXYGEN_FOUND)
	CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/doc/Doxyfile.in ${CMAKE_BINARY_DIR}/docs/Doxyfile)
	ADD_CUSTOM_COMMAND(
		DEPENDS ${CMAKE_BINARY_DIR}/docs/Doxyfile
		OUTPUT  ${CMAKE_BINARY_DIR}/docs/html/index.html
		COMMAND ${DOXYGEN_EXECUTABLE} ${CMAKE_BINARY_DIR}/docs/Doxyfile
		WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
		)
    ADD_CUSTOM_TARGET(doc DEPENDS ${CMAKE_BINARY_DIR}/docs/html/index.html ${CMAKE_BINARY_DIR}/docs/Doxyfile)
ENDIF(DOXYGEN_FOUND)

ENABLE_TESTING()
add_subdirectory(src)


